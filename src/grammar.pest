WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

identifier = @{
    LETTER ~ (LETTER | NUMBER | "_")*
}

type_ = {
    (identifier ~ ("[]")?)
}

keyword_extern = @{"extern"}
keyword_export = @{"export"}

expression_literal_string = @{"\"" ~ (!"\"" ~ ANY)* ~ "\""}

expression_literal = {
    expression_literal_string
}

expression_function_call = {
    identifier ~ "(" ~ (expression ~ ("," ~ expression)* ~ ","?)? ~ ")"
}

expression_variable_reference = { 
    identifier 
}

expression = {
    expression_function_call 
    | expression_literal
    | expression_variable_reference
}

statement = { expression ~ ";" }

argument = { identifier ~ ":" ~ type_ }
argument_list = _{ argument ~ ("," ~ argument_list)? ~ ","? }

function_body_statements = {("{" ~ statement* ~ "}")}
function_body = { function_body_statements | keyword_extern ~ ";" }

function_declaration = {
    (keyword_export)? ~ "fn" ~ identifier ~ "(" ~ argument_list? ~ ")" ~ ":" ~ type_ ~ function_body
}

struct_field = {
    identifier ~ ":" ~ type_
}

struct_declaration = {
    (keyword_export)? ~ "struct" ~ identifier ~ "{" ~ (struct_field ~ ("," ~ struct_field)* ~ ","?)? ~ "}"
}

declaration = {
    function_declaration
    | struct_declaration
}

import = {"import" ~ identifier ~ ("::" ~ identifier)* ~ ";"}

source_file = {
   SOI ~ import* ~ declaration* ~ EOI
}
